class TestPerfectGym is subclass of MyTest
 
operations
 
 	public newGym: () ==> PerfectGym
	newGym() == (
		return new PerfectGym();
	);
	
	-- Test new gym
	private createGym: () ==> ()
	createGym() == (
		dcl gym:PerfectGym := newGym();
		assertEqual(gym.getUsers(), {});
		assertEqual(gym.getClasses(), {});
	);
	
	/*** USE CASE SCENARIO R01 - create user ***/ 
	-- Test add user
	private addUser: () ==> ()
	addUser() == (
			 
			 dcl gym:PerfectGym := newGym();
			 dcl user:Member := new TestUser().newMember();
			 
			 assertTrue( not gym.userExists(user));
			 assertTrue( gym.addUser(user));
			 assertTrue( gym.userExists(user));
			 
			 assertEqual( gym.getUsers(), {user});
	);
	
	/*** USE CASE SCENARIO R03 - user can edit weight and height ***/ 
	-- Test edit user
	private editUser: () ==> ()
	editUser() == (
			 
			 dcl gym:PerfectGym := newGym();
			 dcl user:Member := new TestUser().newMember();
			 
			 assertTrue( gym.addUser(user));
			 assertTrue( gym.userExists(user));
			 	
			 --login
			 assertTrue( gym.loginMember(user.getNumber(), user.getPassword()) = true);
		 	 assertEqual( gym.getLoggedUser(), user); 
		 
			 assertTrue(user.getWeight() = 50);
			 assertTrue(user.getHeight() = 1.67);	
			 
			 gym.editWeight(user, 51);
			 gym.editHeight(user, 1.68);
			 
			 assertTrue(user.getWeight() = 51);
			 assertTrue(user.getHeight() = 1.68);	
			 
	);
	
	/*** USE CASE SCENARIO R10 - create user with referral   
         USE CASE SCENARIO R12 - consult monthly membership fee ***/
	
	-- Test add user with referral
	private addUserReferral: () ==> ()
	addUserReferral() == (
			dcl gym:PerfectGym := newGym();
			dcl user:Member := new TestUser().newMember();
			dcl user2:Member := new TestUser().newMember2();
			dcl user3:Member := new TestUser().newMember3();
			dcl user4:Member := new TestUser().newMember4();
			 
			assertTrue( not gym.userExists(user));
			assertTrue( gym.addUser(user));
			assertTrue( gym.addUser(user2));
			assertTrue( not gym.addUserReferral(user, user2));
			assertTrue( gym.addUserReferral(user, user3));
			assertTrue( gym.userExists(user));
			assertTrue( gym.userExists(user2));
			assertEqual( user.getReferrals(), 1);
			assertTrue( gym.addUserReferral(user, user4));
			
			-- Discount of 1$ per referral 
			assertEqual( user.getMonthly(), 28);
			-- Discount of 20% due to age
			assertEqual( user2.getMonthly(), 24);
			 
			assertEqual( gym.getUsers(), {user, user2, user3, user4});
	);
	
	-- Test add repeated user
	private addRepeatedUser: () ==> ()
	addRepeatedUser() == (
			 
			 dcl gym:PerfectGym := newGym();
			 dcl user:Member := new TestUser().newMember();
			 
			 --number of users is 0
			 assertEqual( card gym.getUsers(), 0);
			 
			 assertTrue( not gym.userExists(user));
			 assertTrue( gym.addUser(user));
			 assertTrue( gym.userExists(user));
			 
			 --number of users is 1
			 assertEqual( card gym.getUsers(), 1);
			 
			 -- not add the user again
			 assertEqual( gym.addUser(user), false);
			 assertEqual( card gym.getUsers(), 1);
	);
	
	/*** USE CASE SCENARIO R09 - add or remove gym classes **/
	-- Test add/remove classes
	private addGymClass: () ==> ()
	addGymClass() == (
	
		dcl gym:PerfectGym := newGym();
		dcl user:Professor := new TestUser().newProfessor();
		dcl gclass:GymClass := new GymClass("cycling", <Cycling>, "cycling class", 10, user, <Monday>, mk_GymClass`Time(15,20), 90);
		assertTrue (gym.addUser(user));
 
		assertEqual(gym.getClasses(), {});
		
		gym.addClass(gclass); --add
		assertEqual(gym.getClasses(), {gclass});
		assertTrue(gym.classRegistered(gclass));
		
		gym.removeClass(gclass); --remove
		assertEqual(gym.getClasses(), {});
	);
 
	-- Add class with same name
	private addGymClassSameName: () ==> ()
	addGymClassSameName() == (
		
		dcl gym:PerfectGym := newGym();
		dcl user:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass := new GymClass("cycling", <Cycling>, "cycling class", 10, user, <Monday>, mk_GymClass`Time(15,20), 90);
		dcl gclass2:GymClass := new GymClass("cycling", <Cycling>, "fit class", 10, user, <Monday>, mk_GymClass`Time(08,20), 90);		
		assertTrue (gym.addUser(user));
		
		assertEqual(gym.getClasses(), {});
		
		gym.addClass(gclass1); --add
		assertEqual(gym.getClasses(), {gclass1});
		
		gym.addClass(gclass2); --breaks pre-condition 
	);
	
	-- Test gym schedule
	private testGymClasses: () ==> ()
	testGymClasses() == (
		
		dcl gym:PerfectGym := newGym();
		dcl user:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass:= new GymClass("cycling",<Cycling>, "cycling class", 10, user, <Monday>, mk_GymClass`Time(15,20), 90);
		dcl gclass2:GymClass:= new GymClass("yoga", <Yoga>, "yoga class", 10, user, <Monday>, mk_GymClass`Time(08,20), 90);		
		
		assertTrue (gym.addUser(user));
 
		assertEqual(gym.getClasses(), {});
		gym.addClass(gclass1);  
		assertEqual(gym.getClasses(), {gclass1});
		gym.addClass(gclass2);  
		assertEqual(gym.getClasses(), {gclass1, gclass2});
 	
	);
	
	
	/*** USE CASE SCENARIO R08 - user can see gym classes 
        USE CASE SCENARIO R11 - user can see gym classes filtered by class type and week day **/
	
	-- Test gym schedule
	private testGymSchedule: () ==> ()
	testGymSchedule() == (
		
		dcl gym:PerfectGym := newGym();
		dcl user:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass:= new GymClass("cycling", <Cycling>, "cycling class", 10, user, <Monday>, mk_GymClass`Time(15,20), 90);
		dcl gclass5:GymClass:= new GymClass("bodyattack", <BodyAttack>, "bodyattack class", 10, user, <Tuesday>, mk_GymClass`Time(15,20), 90);
		dcl gclass2:GymClass := new GymClass("yoga", <Yoga>, "yoga class", 10, user, <Monday>, mk_GymClass`Time(08,00), 45);		
		dcl gclass3:GymClass:= new GymClass("zumba", <Zumba>, "zumba class", 10, user, <Monday>, mk_GymClass`Time(20,30), 90);	
		dcl gclass4:GymClass := new GymClass("cycling2", <Cycling>, "cycling class", 10, user, <Saturday>, mk_GymClass`Time(09,40), 60);	
		dcl gclass6:GymClass:= new GymClass("cycling3", <Cycling>, "cycling class", 10, user, <Monday>, mk_GymClass`Time(08,50), 45);
		dcl schedule: map GymClass`Day_week to seq of GymClass;
		
		assertTrue (gym.addUser(user));

		schedule:= gym.getSchedule();
		
		--empty schedule
		assertEqual(gym.getClasses(), {});
		assertEqual(schedule, {|->});
		
		--add classes
		gym.addClass(gclass1);  
		gym.addClass(gclass2);  
		gym.addClass(gclass3); 
		gym.addClass(gclass4); 
		gym.addClass(gclass5); 
	
		schedule:= gym.getSchedule();
		
		--ordered schedule
		assertEqual(gym.getClasses(), {gclass1, gclass2, gclass3, gclass4, gclass5});
		
		assertEqual(gym.getSchedule(<Monday>), {<Monday>|->[gclass2, gclass1, gclass3]});
		assertEqual(schedule, {<Monday>|->[gclass2, gclass1, gclass3], <Saturday>|->[gclass4], <Tuesday>|->[gclass5]});
		
		-- class shedule 
		assertEqual(gym.getSchedule2(<Cycling>), {<Monday>|->[ mk_GymClass`Time(15,20)], <Saturday>|->[ mk_GymClass`Time(09,40)] });
		gym.addClass(gclass6); 		
		assertEqual(gym.getSchedule2(<Cycling>), {<Monday>|->[ mk_GymClass`Time(08,50), mk_GymClass`Time(15,20)], <Saturday>|->[ mk_GymClass`Time(09,40)] });
		assertEqual(gym.getSchedule2(<BodyAttack>), {<Tuesday>|->[ mk_GymClass`Time(15,20)]});
		assertEqual(gym.getSchedule2(<RPM>), {|->});
		
	);
		
	
	-- Test overlap classes
	private gymClassesTimeOverlap: () ==> ()
	gymClassesTimeOverlap() == (
		
		dcl gym:PerfectGym := newGym();
		dcl user:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass:= new GymClass("cycling",  <Cycling>, "cycling class", 10, user, <Monday>, mk_GymClass`Time(15,20), 90);
		dcl gclass2:GymClass:= new GymClass("yoga", <Yoga>, "yoga class", 10, user, <Monday>, mk_GymClass`Time(16,49), 90);		 	
		
		assertTrue (gym.addUser(user));
		
		assertEqual(gym.getClasses(), {});
		gym.addClass(gclass1);  
		assertEqual(gym.getClasses(), {gclass1});
		gym.addClass(gclass2);  --breaks pre condition
		
	);
 	
 	/*** USE CASE SCENARIO R02 - login and logout user ***/
 	-- Test login
 	private testLogin: () ==> ()
	testLogin() == (
		
		 dcl gym:PerfectGym := newGym();
		 dcl user:Member := new TestUser().newMember(); 
		 assertTrue( gym.addUser(user));
		 assertTrue( gym.userExists(user));
		 assertEqual( gym.getLoggedUser(), nil); 
		 assertTrue( gym.loginMember(user.getNumber(), user.getPassword()) = true);
		 assertEqual( gym.getLoggedUser(), user); 
	 	 gym.logoutMember();
	 	 assertEqual( gym.getLoggedUser(), nil); 
	 	
	);
	
	-- Test failed login
 	private testFailedLogin: () ==> ()
	testFailedLogin() == (
	  	
	   dcl gym:PerfectGym := newGym(); 
		 dcl user:Member := new TestUser().newMember(); 
		 
		 assertEqual( gym.getLoggedUser(), nil); 
		 assertEqual( gym.loginMember(user.getNumber(), user.getPassword()), false);	--user not registered
		 assertEqual( gym.getLoggedUser(), nil); 
		 
		 assertTrue( gym.addUser(user));
		 assertTrue( gym.userExists(user));

		 assertEqual( gym.loginMember(user.getNumber(), "wrongPassword"), false);			--wrong combination
		 assertEqual( gym.getLoggedUser(), nil); 
	 
	);
	
	
	/*** USE CASE SCENARIO R04 - user can enroll in a class
       USE CASE SCENARIO R03 - user can access enrolled classes ***/
	
	-- Test member add classes
	public testAddGymClasses: () ==> ()
	testAddGymClasses() == (
	
	  dcl gym:PerfectGym := newGym();
		dcl prof:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass := new GymClass("cycling", <Cycling>, "cycling class", 10, prof, <Monday>, mk_GymClass`Time(15,20), 90);
		dcl user:Member := new TestUser().newMember(); 
		
		assertTrue( gym.addUser(user));
		assertTrue( gym.addUser(prof));
		gym.addClass( gclass1); 
		
		
		-- 1. login user
		assertEqual( gym.getLoggedUser(), nil); 
		assertEqual( gym.loginMember(user.getNumber(), user.getPassword()), true);
		assertEqual( gym.getLoggedUser(), user);
		
		-- 2. enroll in gym class
		gym.enrollGymClass( user, gclass1);
		
		assertEqual( gclass1.getParticipants(), {user});
		assertEqual( gym.getClasses(user), {gclass1});
 
	);
	
	/*** USE CASE SCENARIO R04 - user can cancel a class **/
	-- Test member remove classes
	public testRemoveGymClasses: () ==> ()
	testRemoveGymClasses() == (
	
	  dcl gym:PerfectGym := newGym();
		dcl prof:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass := new GymClass("cycling", <Cycling>, "cycling class", 10, prof, <Monday>, mk_GymClass`Time(15,20), 90);
		dcl gclass2:GymClass := new GymClass("cycling2",<Cycling>, "cycling class", 10, prof, <Tuesday>, mk_GymClass`Time(07,20), 90);
		
		dcl user:Member := new TestUser().newMember(); 
		assertTrue( gym.addUser(user));
		assertTrue( gym.addUser(prof));
		gym.addClass( gclass1); 
		gym.addClass( gclass2);
		
		
		-- 1. login user
		assertEqual( gym.getLoggedUser(), nil); 
		assertEqual( gym.loginMember(user.getNumber(), user.getPassword()), true);
		assertEqual( gym.getLoggedUser(), user);
		
		-- 2. enroll in gym classes
		gym.enrollGymClass(user, gclass1);
		assertEqual( gclass1.getParticipants(), {user});
		assertEqual( gym.getClasses(user), {gclass1});
		gym.enrollGymClass(user, gclass2);
		assertEqual( gclass2.getParticipants(), {user});
		assertEqual( gym.getClasses(user), {gclass2, gclass1});
 
 		-- 3. remove user from a gym class
 		gym.removeUserGymClass(user, gclass1);
 		assertEqual( gclass1.getParticipants(), {});
 		assertEqual( gym.getClasses(user), {gclass2});
 		assertEqual( gclass2.getParticipants(), {user});
 		
	);
	
	/** USE CASE SCENARIO R11 - user can see gym classes filtered by professor**/
	-- Test professor classes
	public testProfessorClasses: () ==> ()
	testProfessorClasses() == (
	
	  dcl gym:PerfectGym := newGym();
		dcl prof:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass := new GymClass("cycling", <Cycling>, "cycling class", 10, prof, <Monday>, mk_GymClass`Time(15,20), 90);
		dcl gclass2:GymClass := new GymClass("cycling2", <Cycling>, "cycling class", 10, prof, <Tuesday>, mk_GymClass`Time(07,20), 90);
	
		assertTrue( gym.addUser(prof));
		gym.addClass( gclass1); 
		gym.addClass( gclass2);
		
		assertEqual(gym.getClasses(prof), {gclass1, gclass2});
		
	);
	
	-- Test fail enroll class
	public testFailEnrollGymClass: () ==> ()
	testFailEnrollGymClass() == (
		
		dcl gym:PerfectGym := newGym();
		dcl prof:Professor := new TestUser().newProfessor();
		dcl gclass1:GymClass := new GymClass("cycling",<Cycling>, "cycling class", 10, prof, <Monday>, mk_GymClass`Time(15,20), 90);
 
		dcl user:Member := new TestUser().newMember(); 
		
		assertTrue( gym.addUser(user));
		assertTrue( gym.addUser(prof));
		
		-- 1. login user
		assertEqual( gym.getLoggedUser(), nil); 
		assertEqual( gym.loginMember(user.getNumber(), user.getPassword()), true);
		assertEqual( gym.getLoggedUser(), user);
		
		-- 2. enroll in gym classes
		gym.enrollGymClass(user, gclass1);				-- breaks precondition (class not in the system)
 			
	 	gym.logoutMember();
	 	assertEqual( gym.getLoggedUser(), nil); 
 		
 		gym.addClass( gclass1); 
 		gym.enrollGymClass(user, gclass1);				-- breaks precondition (user not logged in)
 		 		
	);
	
	
	
	/*** USE CASE SCENARIO R05 - professor can create a training plan for a member      
			 USE CASE SCENARIO R03 - user can access training plan
       USE CASE SCENARIO R06 - create exercises
       USE CASE SCENARIO R07 - add or remove exercises from plan ***/
	
	-- Test add training plan
	public testAddTrainingPlan: () ==> ()
	testAddTrainingPlan() == (
		
		dcl gym:PerfectGym := newGym();
		dcl prof:Professor := new TestUser().newProfessor();	 
		dcl user:Member := new TestUser().newMember(); 
 
		dcl exercise1:Exercise:= new Exercise(4, 10, <Leg>, "leg workout");
		dcl exercise2:Exercise:= new Exercise(2, 15, <Arm>, "arm workout");
		dcl plan:Plan := new Plan([exercise1, exercise2], prof);
	 
		assertTrue( gym.addUser(user));
		assertTrue( gym.addUser(prof));
		
		-- 1. login prof
		assertEqual( gym.getLoggedUser(), nil); 
		assertEqual( gym.loginMember(prof.getNumber(), prof.getPassword()), true);
		assertEqual( gym.getLoggedUser(), prof);
		
		-- 2. add that training plan to the user
		gym.createTrainingPlan(prof, user, plan);
		
		-- 3. user check the training plan
	 	gym.logoutMember();
	 	assertEqual( gym.getLoggedUser(), nil); 
 		assertEqual( gym.loginMember(user.getNumber(), user.getPassword()), true);
 		assertEqual( gym.getLoggedUser(), user);
 		
 		assertEqual(gym.getPlan(user), plan);
 	
	);
	
	-- Test add training plan
	public testPermissions: () ==> ()
	testPermissions() == (
		
		dcl gym:PerfectGym := newGym();
		dcl prof:Professor := new TestUser().newProfessor();	 
		dcl user:Member := new TestUser().newMember(); 
 		dcl user2:Member := new TestUser().newMember(); 
 		
		dcl exercise1:Exercise:= new Exercise(4, 10, <Leg>, "leg workout");
		dcl exercise2:Exercise:= new Exercise(2, 15, <Arm>, "arm workout");
		dcl plan:Plan := new Plan([exercise1, exercise2], prof);
	 
		assertTrue( gym.addUser(user));
		assertTrue( gym.addUser(user2));
		assertTrue( gym.addUser(prof));
		
		-- user login
		assertEqual( gym.getLoggedUser(), nil); 
 		assertEqual( gym.loginMember(user.getNumber(), user.getPassword()), true);
 		assertEqual( gym.getLoggedUser(), user);
		
		-- add that training plan to the user
		gym.createTrainingPlan(prof, user, plan); --breaks precondition
 	
 		assertEqual(gym.getPlan(user), nil);
 		assertEqual(gym.getPlan(user2), nil);	--breaks precondition (cant see other members plan)
 	
	);
 	
	public test: () ==> ()
	test() == (
	 		
	 	 createGym();
		 addUser();
		 editUser();
		 addUserReferral();
		 addRepeatedUser();
		 addGymClass();
		 testGymClasses();
		 testGymSchedule(); 
		 testLogin();
		 testFailedLogin();
		 testAddGymClasses();
		 testRemoveGymClasses();
 		 testAddTrainingPlan();
 		 testProfessorClasses();
		 
		 /***** TEST CASES WITH INVALID INPUTS ******/
		 --addGymClassSameName();
		 --gymClassesTimeOverlap();
		 --testFailEnrollGymClass();
		 --testPermissions();
	);

 
end TestPerfectGym